grammar =       - r:root
               ( declaration | d:definition { addRuleDefinitionToGrammar(r, d); } )+
               trailer? end-of-file
               { printTree(r); /*writeTree(r);*/ }

root = { $$ = newGrammar(); }

declaration =   '%{' < ( !'%}' . )* > RPERCENT

trailer =       '%%' < .* >

definition =    i:identifier EQUAL e:expression SEMICOLON?      { $$ = newDefinition(i, e); }

expression =    s1:sequence (
                    BAR s2:sequence                             { s1 = newBinary(Alternation, s1, s2); } 
                )*                                              { $$ = s1; }

# Was error+ before, rewritten for reasons
sequence =      e:error (
                    f:error                                     { e = newBinary(Sequence, e, f); }
                )*                                              { $$ = e; }

error =         p:prefix ( TILDE action )?                      { $$ = p; }

prefix =        AND action
                |
                ( AND s:suffix                                  { $$ = newUnary(And, s); }
                | NOT s:suffix                                  { $$ = newUnary(Not, s); }
                | s:suffix                                      { $$ = s; }
                )
#                | ip:initPrefix
#                ( AND                                           { ip = '&'; }
#                | NOT                                           { ip = '!'; }
#                )? s:suffix                                     {
#                                                                    if (ip == '&')     { $$ = newUnary(And, s); }
#                                                                    if (ip == '!')     { $$ = newUnary(Not, s); }
#                                                                    else                    { $$ = s; }
#                                                                }

#initPrefix = { $$ = 0; }

suffix =        p:primary
                ( QUERY                                         { p = newUnary(Optional, p); }
                | STAR                                          { p = newUnary(Star, p); }
                | PLUS                                          { p = newUnary(Plus, p); }
                )?                                              { $$ = p; }

primary =       i1:identifier COLON i2:ruleCallIdent !EQUAL     { $$ = newAssignment(i1, i2); }
|               i:ruleCallIdent !EQUAL                          { $$ = i; }
|               OPEN e:expression CLOSE                         { $$ = e; }
|               l:literal                                       { $$ = l; }
|               c:class                                         { $$ = c; }
|               DOT                                             { $$ = newDot(); }
|               a:action                                        { $$ = a; }
|               BEGIN                                           { $$ = newBegin(); }
|               END                                             { $$ = newEnd(); }

identifier =    < [-a-zA-Z_][-a-zA-Z_0-9]* > -                  { $$ = newString(yytext); }

ruleCallIdent = < [-a-zA-Z_][-a-zA-Z_0-9]* > -                  { $$ = newIdentifier(yytext); }

literal =       ['] < ( !['] char )* > ['] -                    { $$ = newString(yytext); }
|               ["] < ( !["] char )* > ["] -                    { $$ = newString(yytext); }

class =         '[' < ( !']' range )* > ']' -                   { $$ = newCharacterClass(yytext); }

range =         char '-' char | char

char =          '\\' [abefnrtv'"\[\]\\]
|               '\\' [0-3][0-7][0-7]
|               '\\' [0-7][0-7]?
|               !'\\' .

action =        '{' < braces* > '}' -                           { $$ = newAction(yytext, NULL); }

braces =        '{' braces* '}'
|               !'}' .

EQUAL =         '=' -
COLON =         ':' -
SEMICOLON =     ';' -
BAR =           '|' -
AND =           '&' -
NOT =           '!' -
QUERY =         '?' -
STAR =          '*' -
PLUS =          '+' -
OPEN =          '(' -
CLOSE =         ')' -
DOT =           '.' -
BEGIN =         '<' -
END =           '>' -
TILDE =         '~' -
RPERCENT =      '%}' -

- =             ( space | comment )*
space =         ' ' | '\t' | end-of-line
comment =       '#' ( !end-of-line . )* end-of-line
end-of-line =   '\r\n' | '\n' | '\r'
end-of-file =   !.

